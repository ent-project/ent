
allprojects {

    apply plugin: 'java'
    apply plugin: 'eclipse'

    eclipse {
        classpath {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

ext {
    mockitoVersion = '5.6.0'
    junitVersion = '5.10.0'
    logbackVersion = '1.4.11'
    guavaVersion = '32.1.3-jre'
    apacheCommonsLangVersion = '3.12.0'
    apacheCommonsTextVersion = '1.10.0'
    apacheCommonsMathVersion = '3.6.1'
}

subprojects {

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.7'
        implementation group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
        implementation group: 'ch.qos.logback', name: 'logback-core', version: logbackVersion
        implementation group: 'org.apache.commons', name:'commons-rng-core', version: '1.5'
        implementation group: 'org.apache.commons', name:'commons-rng-simple', version: '1.5'

        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: junitVersion
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitVersion
        testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.24.2'
        testImplementation group: 'nl.jqno.equalsverifier', name: 'equalsverifier', version: '3.15.2'
        testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
        testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: mockitoVersion
    }

    test {
        useJUnitPlatform()
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs += '--enable-preview'
    }

    tasks.withType(JavaExec) {
        jvmArgs += "--enable-preview"
    }

    tasks.withType(Test) {
        jvmArgs += "--enable-preview"
    }
}

// task for gradle wrapper upgrade: change gradleVersion here, then run "./gradlew wrapper"
tasks.named('wrapper') {
   gradleVersion = '8.4'
   distributionType = Wrapper.DistributionType.ALL
}
